// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/CPU.hdl

/**
 * The Hack CPU (Central Processing unit), consisting of an ALU,
 * two registers named A and D, and a program counter named PC.
 * The CPU is designed to fetch and execute instructions written in 
 * the Hack machine language. In particular, functions as follows:
 * Executes the inputted instruction according to the Hack machine 
 * language specification. The D and A in the language specification
 * refer to CPU-resident registers, while M refers to the external
 * memory location addressed by A, i.e. to Memory[A]. The inM input 
 * holds the value of this location. If the current instruction needs 
 * to write a value to M, the value is placed in outM, the address 
 * of the target location is placed in the addressM output, and the 
 * writeM control bit is asserted. (When writeM==0, any value may 
 * appear in outM). The outM and writeM outputs are combinational: 
 * they are affected instantaneously by the execution of the current 
 * instruction. The addressM and pc outputs are clocked: although they 
 * are affected by the execution of the current instruction, they commit 
 * to their new values only in the next time step. If reset==1 then the 
 * CPU jumps to address 0 (i.e. pc is set to 0 in next time step) rather 
 * than to the address resulting from executing the current instruction. 
 */

CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
	Mux16(a=instruction,b=aluOutput,sel=instruction[15],out=toARegister);
	
	Not(in=instruction[5],out=notd1);
	Nand(a=instruction[15],b=notd1,out=aRegisterLoader);
	ARegister(in=toARegister,load=aRegisterLoader,out=aRegister,out[0..14]=addressM,out=toPC); 
	
	Mux16(a=aRegister,b=inM,sel=instruction[12],out=amRegister); 
	
	ALU(x=dRegister,y=amRegister,zx=instruction[11],nx=instruction[10],zy=instruction[9],ny=instruction[8],f=instruction[7],no=instruction[6],out=aluOutput,out=outM,out=dDest,zr=zero,ng=neg); 
	
	And(a=instruction[4],b=instruction[15],out=dRegisterLoader); 
	DRegister(in=dDest,load=dRegisterLoader,out=dRegister);
	
	
	Or(a=zero, b=neg, out=zeroOrNeg);
	Not(in=zeroOrNeg, out=pos);
	
	And(a=instruction[2], b=instruction[15], out=j1Loader);
	And(a=instruction[1], b=instruction[15], out=j2Loader);
	And(a=instruction[0], b=instruction[15], out=j3Loader);
	
	And(a=neg, b=j1Loader, out=andNegJ1Loader);
	And(a=zero, b=j2Loader, out=andNegJ2Loader);
	And(a=pos, b=j3Loader, out=andNegJ3Loader);
	
	Or(a=andNegJ1Loader, b=andNegJ2Loader, out=orNegJ1J2Loader);
	Or(a=orNegJ1J2Loader, b=andNegJ3Loader, out=pcLoader);
	
	Not(in=pcLoader, out=pcInc);
	
	PC(in=toPC,load=pcLoader,inc=pcInc,reset=reset,out[0..14]=pc); 
	
	And(a=instruction[3],b=instruction[15],out=writeM); 
}